plugins {
    id 'java'
    id 'org.springframework.boot' version '3.0.8'
    id 'io.spring.dependency-management' version '1.0.15.RELEASE'
    id 'checkstyle'
    id 'jacoco'
}

checkstyle {
    toolVersion = 7.5
    configFile = rootProject.file('config/checkstyle/checkstyle.xml')
    configProperties = [samedir: "${rootDir}/config/checkstyle/"]
    checkstyleTest {}.enabled = false
}

group = 'com.mercedes.crm'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

repositories {
    mavenCentral()
}

ext {
    set('springCloudVersion', "2022.0.3") //cloud版本需要与SpringBoot匹配：https://spring.io/projects/spring-cloud
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

dependencies {
    implementation 'org.springframework.cloud:spring-cloud-starter-openfeign'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    implementation 'org.springframework.data:spring-data-commons'

    implementation 'org.mapstruct:mapstruct:1.4.2.Final'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.4.2.Final'
    compileOnly 'org.projectlombok:lombok:1.18.20'
    annotationProcessor 'org.projectlombok:lombok:1.18.20'

    //SpringBoot3使用SpringDoc-openapi v2版本, webmv-ui没有对应v1版本所以必须像如下一样单独配置两个引用
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.0.2'

    //SpringBoot2 for springdoc v1
    //implementation 'org.springdoc:springdoc-openapi-ui:1.7.0'
    //implementation 'org.springdoc:springdoc-openapi-webmvc-core:1.7.0'

    implementation 'javax.servlet:javax.servlet-api:4.0.1'
    implementation 'com.h2database:h2:2.0.204'
    implementation 'mysql:mysql-connector-java:8.0.33'

    // 9.17.0 only support mysql 8
    implementation 'org.flywaydb:flyway-core:9.17.0'
    implementation 'org.flywaydb:flyway-mysql:9.17.0'

    implementation 'org.mybatis.spring.boot:mybatis-spring-boot-starter:3.0.1'
}

test {
    ignoreFailures = false
    finalizedBy jacocoTestReport
    useJUnitPlatform()
}

jacocoTestReport {
    dependsOn test // tests are required to run before generating the report
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it,
                    exclude: [
                            'com/mercedes/crm/common/Result**',
                            'com/mercedes/crm/common/GlobalExceptionHandler**',
                            'com/mercedes/crm/common/SpringDocConfig**',
                            'com/mercedes/crm/domain/repository/**',
                            'com/mercedes/crm/infrastructure/adapter/acl/dto/**',
                            'com/mercedes/crm/infrastructure/adapter/ohs/dto/**',
                            'com/mercedes/crm/infrastructure/mock/**'
                    ])
        }))
    }
}

jacocoTestCoverageVerification {
    dependsOn test // tests are required to run before generating the report
    violationRules {
        rule {
            limit {
                minimum = 0.7
            }
        }
    }
}
